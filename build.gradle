// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.30"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android')?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion Config.sdk_version

                defaultConfig {
                    minSdkVersion Config.min_sdk_version
                    targetSdkVersion Config.sdk_version
                }

                buildFeatures {
                    viewBinding true
                }

                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility Config.java_version
                    targetCompatibility Config.java_version
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}